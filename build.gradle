plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'fr.litarvan'
version = '3.1.4-FLOWARGVERSION'
archivesBaseName = 'openlauncherlib'

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

jar {
    from "LICENSE"
    from "LICENSE.LESSER"
    from {
        configurations.compileClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            pom {
                groupId = project.group
                version = project.version
                artifactId = archivesBaseName
                name = 'OpenLauncherLib'
                description = 'A simple modern java launching library with minecraft support '
                url = 'https://github.com/FlowArg/OpenLauncherLib'

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/FlowArg/OpenLauncherLib/issues'
                }

                licenses {
                    license {
                        name = 'LICENSE'
                        url = 'https://github.com/FlowArg/OpenLauncherLib/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'OpenLauncherLib'
        userOrg = 'flowarg'
        licenses = ['GPL-3.0']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/FlowArg/OpenLauncherLib.git'
        version {
            name = 'v' + project.version
            released = new Date()
            vcsTag = 'v' + project.version
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    api 'org.json:json:20200518'
}
